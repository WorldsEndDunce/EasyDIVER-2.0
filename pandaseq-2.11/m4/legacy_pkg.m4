# pkg.m4 - Macros to make old packages look like pkg-config.            -*- Autoconf -*-
# 
# Copyright © 2014 Andre Masella <andre@masella.name>.
# Copyright © 2004 Scott James Remnant <scott@netsplit.com>.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
#
# As a special exception to the GNU General Public License, if you
# distribute this file as part of a program that contains a
# configuration script generated by Autoconf, you may include it under
# the same distribution terms that you use for the rest of that program.

# LEGACY_CHECK_MODULES(VARIABLE-PREFIX, HEADER, LIBRARY, FUNCTION, [OTHER-LIBS],
# [ACTION-IF-FOUND], [ACTION-IF-NOT-FOUND])
# --------------------------------------------------------------
AC_DEFUN([LEGACY_CHECK_MODULES],
[AC_ARG_VAR([$1][_CFLAGS], [C compiler flags for $1])dnl
AC_ARG_VAR([$1][_LIBS], [linker flags for $1])dnl

pkg_original_libs="$LIBS"
AC_CHECK_HEADERS([$2], [pkg_failed=no], [pkg_failed=yes])
if test $pkg_failed = no; then
	AC_CHECK_LIB([$3], [$4], [], [pkg_failed=yes], [$5])
fi

m4_define([_PKG_TEXT], [Alternatively, you may set the environment variables $1[]_CFLAGS
and $1[]_LIBS.])

if test $pkg_failed = yes; then
  pkg_failed=yes # make sure this block isn't empty
  $7
else
	$1[]_CFLAGS=""
	$1[]_LIBS="$LIBS"
  LIBS="$pkg_original_libs"
	$6
fi[]dnl
])# LEGACY_CHECK_MODULES
